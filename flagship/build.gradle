import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'com.vanniktech.maven.publish' version "0.30.0"
}

apply plugin: 'signing'
apply from: 'jacoco.gradle'

kotlin {
    jvmToolchain(11)
}

android {
    publishNonDefault true
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    testOptions {

        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true


        unitTests {
            all {
                jvmArgs '-noverify'
            }
        }
    }

    buildTypes {
        debug {
            kotlinOptions {
                freeCompilerArgs = ["-Xdebug"]
            }
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        all {
            buildConfigField "String", "FLAGSHIP_VERSION_NAME", "\"${flagship_version_name}\""
            buildConfigField "int", "FLAGSHIP_VERSION_CODE", "${flagship_version_code}"
            resValue("integer", "ABTastyVersionCode", "${flagship_version_code}")
            resValue("string", "ABTastyVersion", "${flagship_version_name}")
        }
        jacoco {
            version = '0.8.11'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "flagship-android-${flagship_version_name}.aar"
        }
    }
    namespace 'com.abtasty.flagship'
}

configurations {
    common
    compat
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")
    implementation 'androidx.appcompat:appcompat:1.7.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.14.1'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0"

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'androidx.lifecycle:lifecycle-process:2.8.7'

    annotationProcessor("androidx.room:room-compiler:2.6.1")
    ksp 'androidx.room:room-compiler:2.6.1'
}


repositories {
    google()
    mavenCentral()
}

//apply from: 'flagship-publishing.gradle'

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates(maven_group_id, maven_artifact_id, flagship_version_name)

    pom {
        name = maven_artifact_id
        description = 'Visit https://developers.flagship.io/ to get started with Flagship.'
        url = 'https://github.com/flagship-io/flagship-android'
        licenses {
            license {
                name = 'Apache License 2.0'
                url = 'https://github.com/flagship-io/flagship-android/blob/master/LICENSE'
            }
        }
        developers {
            developer {
                id = 'raf-abtasty'
                name = 'Raphael'
                email = 'raphael@abtasty.com'
            }
        }
        scm {
            connection = 'scm:git:github.com/flagship-io/flagship-android.git'
            developerConnection = 'scm:git:ssh:github.com/flagship-io/flagship-android.git'
            url = 'https://github.com/flagship-io/flagship-android/blob/master/'
        }
    }
}