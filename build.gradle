apply plugin: 'io.github.gradle-nexus.publish-plugin'
buildscript {

    /*
        ./gradlew publishToSonatype closeSonatypeStagingRepository
        ./gradlew publishToSonatype -Dvariant=compat  closeSonatypeStagingRepository
     */

    ext {
        kotlin_version = '1.8.20'
        maven_artifact_id = "flagship-android"
        maven_repo = "flagship-android"
        maven_group_id = "com.abtasty"
        maven_user_org = "abtasty"
        maven_variant = System.getProperty("variant", "")
        if (!maven_variant.isEmpty()) {
            maven_artifact_id = maven_artifact_id + '-' + maven_variant
        }
        flagship_version_name = System.getenv('FLAGSHIP_VERSION_NAME') ?: "3.0.6"
        flagship_version_code = System.getenv('FLAGSHIP_VERSION_CODE') ?: 16
        sonatype_signing_key = System.getenv('SONATYPE_SIGNING_KEY')
        sonatype_signing_pwd = System.getenv('SONATYPE_SIGNING_PWD')
        sonatype_username = System.getenv('SONATYPE_USERNAME') ?: ossrhUsername
        sonatype_password = System.getenv('SONATYPE_PASSWORD') ?: ossrhPassword
        sonatype_repository_id = System.getenv('SONATYPE_REPOSITORY') ?: stagingRepositoryId
    }

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.24.18"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            stagingProfileId = sonatype_repository_id
            username = sonatype_username
            password = sonatype_password
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

